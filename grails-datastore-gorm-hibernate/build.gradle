configurations {
    optional
}

dependencies {
    compile(project(":grails-datastore-gorm-hibernate-core"))

    // Web dependencies optional
    optional("org.grails:grails-web:$grailsVersion") {
        transitive = false
    }
    optional("org.grails:grails-spring:$grailsVersion") {
        transitive = false
    }
    optional("org.springframework:spring-webmvc:$springVersion") {
        transitive = false
    }
    optional 'javax.servlet:servlet-api:2.5'
    optional(project(":grails-datastore-web"))


    testCompile project(":grails-datastore-gorm-test")
    testCompile project(":grails-datastore-gorm-tck")
    testCompile "com.h2database:h2:1.3.164"

    testCompile "net.sf.ehcache:ehcache-core:2.4.6"
    testCompile "org.hibernate:hibernate-ehcache:3.6.10.Final"
}

sourceSets {
    main {
        compileClasspath += configurations.optional
    }
    javadoc {
        classpath = configurations.compile + configurations.optional
    }
}
idea {
    module {
        scopes.PROVIDED.plus += configurations.optional
    }
}

eclipse {
    classpath {
        plusConfigurations += configurations.optional
    }
}

test {
    testLogging {
        exceptionFormat ='full'
    }
    forkEvery = 30
    maxParallelForks = 4
    jvmArgs '-server','-Xmx1024M', '-Xms64M', '-XX:PermSize=32m','-XX:MaxPermSize=256m','-XX:+CMSClassUnloadingEnabled','-XX:+HeapDumpOnOutOfMemoryError'
}

compileTestGroovy {
    groovyOptions.fork(memoryMaximumSize: '1024m')
}
/*
test {
	jvmArgs '-Xmx1024m', '-Xdebug', '-Xnoagent', '-Dgrails.full.stacktrace=true', '-Djava.compiler=NONE',
	        '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
}
*/


