def grailsVersion = '2.5.1'
configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if(details.requested.group == 'org.grails' && !details.requested.name.startsWith('grails-datastore')) {
                 details.useVersion(grailsVersion)
            }
        }
    }
    resolutionStrategy.dependencySubstitution {
        // Substitute project and module dependencies
        substitute project(':grails-datastore-gorm-support') with project(':grails-datastore-gorm-grails2-support')
    }
}
dependencies {

    def excludes = {
        exclude group:"org.grails",module: "grails-plugin-url-mappings"
        exclude group:"org.grails",module: "grails-plugin-servlets"
        exclude group:"org.grails",module: "grails-plugin-controllers"
        exclude group:"org.grails",module: "grails-plugin-domain-class"
        exclude group:"org.grails",module: "grails-plugin-gsp"
        exclude group:"org.grails",module: "grails-plugin-filters"
        exclude group:"org.grails",module: "grails-plugin-interceptors"
        exclude group:"org.grails",module: "grails-plugin-mimetypes"
        exclude group:"org.grails",module: "grails-plugin-converters"
        exclude group:"org.grails",module: "grails-logging"
        exclude group:"org.grails",module: "grails-test"
        exclude group:"org.grails",module: "grails-datastore-gorm"
        exclude group:"org.grails",module: "grails-datastore-core"
        exclude group:"org.grails",module: "grails-datastore-simple"
        exclude group:"org.grails",module: "grails-datastore-gorm"
    }

    compile "org.grails:grails-test:$grailsVersion", excludes
    compile "org.grails:grails-plugin-testing:$grailsVersion", excludes

    compile project(":grails-datastore-gorm-grails2-support")
    compile project(":grails-datastore-gorm"), {
        exclude group:'org.grails',module:'grails-datastore-gorm-support'
        exclude group:'org.grails',module:'grails-datastore-gorm-plugin-support'
    }
    compile project(":grails-datastore-simple"),
            project(":grails-datastore-core")

    compile "org.grails:grails-test:$grailsVersion",excludes
    compile "org.grails:grails-core:$grailsVersion",excludes
    compile("org.grails:grails-bootstrap:$grailsVersion",excludes)

    testCompile project(":grails-datastore-gorm-tck"), {
        exclude group:'org.grails',module:'grails-datastore-gorm-support'
        exclude group:'org.grails',module:'grails-datastore-gorm-plugin-support'
    }
    testRuntime "javax.servlet:javax.servlet-api:$servletApiVersion"
    testRuntime "org.grails:grails-web:$grailsVersion",excludes
    testRuntime "log4j:log4j:1.2.16"
}
