dependencies {

    compile project(":grails-datastore-core")
//    compile "org.grails:grails-test:$grailsVersion", {
//        exclude module: "grails-datastore-gorm"
//    }

    compile "org.grails:grails-core:$grailsVersion"
    compile("org.grails:grails-bootstrap:$grailsVersion") {
        transitive = false
    }
    compile "org.grails:grails-async:$grailsVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
}

def astTransformPath
jar.doFirst {
    def metaInfPath = "${compileGroovy.destinationDir}/META-INF/services"

    ant.mkdir(dir:metaInfPath)
    astTransformPath = new File(metaInfPath, "org.codehaus.groovy.transform.ASTTransformation")
    astTransformPath.text = "org.grails.datastore.gorm.query.transform.GlobalDetachedCriteriaASTTransformation"
}
jar.doLast {
    if(astTransformPath) {
        ant.delete file:astTransformPath
    }
}

eclipse {
    classpath {
        file {
            whenMerged { classpath ->
                 def testSrc = classpath.entries.find { entry -> entry.kind == 'src' && entry.path == "src/test/groovy" }
                 if (testSrc) {
                     // greclipse cannot use ASTs that are in the same project, so exclude this from compilation
                     testSrc.excludes = ["**/DirtyCheckTransformationSpec.groovy"]
                 }
            }
        }
    }
}